# Типы
TYPE -> int
TYPE -> float
TYPE_ADV -> TYPE
TYPE_ADV -> void

# Операции
OPER -> +
OPER -> -
OPER -> *
OPER -> ==
OPER -> !=
OPER -> <
OPER -> >

# Операции присваивания
OPER_ASS -> =
OPER_ASS -> +=
OPER_ASS -> -=
OPER_ASS -> *=

# Начальный символ
S -> PROG

# Программа
PROG -> TYPE_ADV main ( ) { BODY }
PROG -> eps

# Тело программы
BODY -> DECLAR BODY		# Объявление переменной
BODY -> ASSIGN BODY		# Присваивание
BODY -> eps

# Для работы с переменными, константами и массивами
VAR -> var INDEX		# Имя переменной с индексом
VAR_ADV -> VAR			# Или переменная
VAR_ADV -> const		# Или константа
INDEX -> eps			# Индекса либо нет (переменная)
INDEX -> [ EXPR ]		# Либо есть (массив)

# Присваивание
ASSIGN -> VAR OPER_ASS EXPR ;	# Переменная, операция, выражение, ";"
EXPR -> - E_1			# Может быть с унарным минусом
EXPR -> E_1			# Или же без него
E_1 -> VAR_ADV E_2		# Переменная / константа и дальнейшие операции
E_1 -> ( EXPR ) E_2		# Может быть выражение в скобках
E_2 -> eps			# Может ничего не быть
E_2 -> OPER E_1			# А может быть знак операции и продолжение выражения

# Объявление переменной
DECLAR -> TYPE D_1 ;		# Начинается с типа переменной / массива
D_1 -> VAR A_1 D_2		# Потом обязательно имя / массива
A_1 -> = EXPR			# Может быть сразу с присваиванием
A_1 -> eps			# А может и без
D_2 -> , D_1			# Могут быть еще объявления
D_2 -> eps			# А могут и не быть

